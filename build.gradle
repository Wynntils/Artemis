plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    id "com.diffplug.spotless" version "6.3.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()

        accessWidenerPath = file("src/main/resources/wynntils.accessWidener")

        runs {
            client {
                // These arguments allow for optional authentication with Mojang servers.
                // If you want to authenticate, put these properties in GRADLE_HOME/gradle.properties.
                // By default, this is C:\Users\<your username>\.gradle\gradle.properties on Windows or ~/.gradle/gradle.properties on Linux/MacOS.
                if (project.hasProperty('mc_uuid') && project.hasProperty('mc_username') && project.hasProperty('mc_accessToken')) {
                    // Your UUID, trimmed / without the dashes
                    programArgs '--uuid', project.getProperty('mc_uuid')
                    // Your Minecraft in-game username, not email
                    programArgs '--username', project.getProperty('mc_username')
                    // Your current access token. When it expires, you need to retrieve a new one and regenerate your run configurations.
                    // You may be able to find it in your .minecraft folder in launcher_accounts.json or launcher_profiles.json.
                    programArgs '--accessToken', project.getProperty('mc_accessToken')
                }
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }
}

def versionObj = new Version(major: 1, minor: 1, patch: 0)

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "com.diffplug.spotless"

    archivesBaseName = rootProject.archives_base_name
    version = versionObj.toString(rootProject.minecraft_version)

    repositories {
        maven { url "https://maven.parchmentmc.org/" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(rootProject.java_version))
        }
        withSourcesJar()
    }

    spotless {
        java {
            // define the steps to apply to Java source code
            importOrder()
            removeUnusedImports()
            palantirJavaFormat('2.17.0')
            licenseHeader '/*\n' +
                    ' * Copyright Â© Wynntils $YEAR.\n' +
                    ' * This file is released under AGPLv3. See LICENSE for full license details.\n' +
                    ' */'
        }
        format 'misc', {
            // define the files to apply `misc` to
            target '*.gradle', '*.md', '.gitignore', '*.properties', "*.json"

            // define the steps to apply to those files
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
    }
}

class Version {
    int major, minor, patch

    static String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    }

    //If someone can find a way to access the mcVersion without using it as a param please do
    String toString(Object minecraft_version) {
        "${major}.${minor}.${patch}+${this.build}.MC${minecraft_version}"
    }
}
